/*
 *  Copyright OpenSearch Contributors
 *  SPDX-License-Identifier: Apache-2.0
 */
import org.opensearch.gradle.info.BuildParams

apply plugin: 'opensearch.build'
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'io.freefair.lombok'

assemble.enabled = false

application {
    mainClass = 'org.openjdk.jmh.Main'
}

test.enabled = false

repositories {
    mavenLocal()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    // This will take root project as the dependency
    api(project(':'))
    api "org.openjdk.jmh:jmh-core:$versions.jmh"
    annotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:$versions.jmh"
    // Dependencies of JMH
    runtimeOnly 'net.sf.jopt-simple:jopt-simple:5.0.4'
    runtimeOnly 'org.apache.commons:commons-math3:3.6.1'
}

// enable the JMH's BenchmarkProcessor to generate the final benchmark classes
// needs to be added separately otherwise Gradle will quote it and javac will fail
compileJava.options.compilerArgs.addAll(["-processor", "org.openjdk.jmh.generators.BenchmarkProcessor"])


run {
    // This is required for C++ code
    systemProperty "java.library.path", "$rootDir/jni/release"
    executable = "${BuildParams.runtimeJavaHome}/bin/java"
    var jvmHeapSize = System.getProperty("jvm.heap.size", "6g")
    jvmArgs("-Xms" + jvmHeapSize, "-Xmx"  + jvmHeapSize)
}


// No licenses for our benchmark deps (we don't ship benchmarks)
tasks.named("dependencyLicenses").configure { it.enabled = false }
dependenciesInfo.enabled = false

thirdPartyAudit.ignoreViolations(
        // these classes intentionally use JDK internal API (and this is ok since the project is maintained by Oracle employees)
        'org.openjdk.jmh.util.Utils'
)

spotless {
    java {
        // IDEs can sometimes run annotation processors that leave files in
        // here, causing Spotless to complain. Even though this path ought not
        // to exist, exclude it anyway in order to avoid spurious failures.
        targetExclude 'src/main/generated/**/*.java'
    }
}

